global isr_keyboard
global isr_timer

global isr0,  isr1,  isr2,  isr3,  isr4,  isr5,  isr6,  isr7
global isr8,  isr9,  isr10, isr11, isr12, isr13, isr14, isr15
global isr16, isr17, isr18, isr19, isr20, isr21, isr22, isr23
global isr24, isr25, isr26, isr27, isr28, isr29, isr30, isr31

extern keyboard_handler
extern timer_handler
extern exception_handler

; Исключение БЕЗ error code — пушим 0 как заглушку
%macro ISR_NOERR 1
isr%1:
    cli
    push dword 0
    push dword %1
    jmp isr_common_stub
%endmacro

; Исключение С error code — CPU уже запушил его, пушим только номер
%macro ISR_ERR 1
isr%1:
    cli
    push dword %1
    jmp isr_common_stub
%endmacro

ISR_NOERR 0   ; #DE  Divide Error
ISR_NOERR 1   ; #DB  Debug
ISR_NOERR 2   ;      NMI
ISR_NOERR 3   ; #BP  Breakpoint
ISR_NOERR 4   ; #OF  Overflow
ISR_NOERR 5   ; #BR  Bound Range
ISR_NOERR 6   ; #UD  Invalid Opcode
ISR_NOERR 7   ; #NM  No Math Coprocessor
ISR_ERR   8   ; #DF  Double Fault        (error code всегда 0)
ISR_NOERR 9   ;      Coprocessor Overrun
ISR_ERR   10  ; #TS  Invalid TSS
ISR_ERR   11  ; #NP  Segment Not Present
ISR_ERR   12  ; #SS  Stack Fault
ISR_ERR   13  ; #GP  General Protection
ISR_ERR   14  ; #PF  Page Fault
ISR_NOERR 15  ;      Reserved
ISR_NOERR 16  ; #MF  x87 FPU Error
ISR_ERR   17  ; #AC  Alignment Check
ISR_NOERR 18  ; #MC  Machine Check
ISR_NOERR 19  ; #XM  SIMD Exception
ISR_NOERR 20  ; #VE  Virtualization
ISR_NOERR 21  ;      Reserved
ISR_NOERR 22
ISR_NOERR 23
ISR_NOERR 24
ISR_NOERR 25
ISR_NOERR 26
ISR_NOERR 27
ISR_NOERR 28
ISR_NOERR 29
ISR_ERR   30  ; #SX  Security Exception
ISR_NOERR 31  ;      Reserved

isr_common_stub:
    pushad          ; сохраняем все регистры (edi,esi,ebp,esp,ebx,edx,ecx,eax)
    push esp        ; передаём указатель на Registers в exception_handler
    call exception_handler
    add esp, 4
    popad
    add esp, 8      ; убираем int_no и err_code
    iret

isr_keyboard:
    cli
    pushad
    call keyboard_handler
    popad
    sti
    iret

isr_timer:
    cli
    pushad
    call timer_handler
    popad
    sti
    iret
